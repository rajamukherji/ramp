PLATFORM := defined("PLATFORM") or shell("uname"):trim
DEBUG := defined("DEBUG")

CFLAGS := []
LDFLAGS := []
PREBUILDS := []

pkgconfig := fun(Args) do
	expr('pkg-config {Args}') => fun() shell('pkg-config', Args):trim
end

c_compile := fun(Source, Object) do
	execute('cc -c {CFLAGS} -o{Object} {Source}')
end

c_includes := fun(Target) do
	var Lines := shell('cc -c {CFLAGS} -M -MG {Target:source}')
	Lines := Lines:replace(r"\\\n ", "")
	Lines := Lines[Lines:find(": ") + 2, 0]:trim
	var Files := Lines / r"[^\\]( +)"
	for File in Files do
		File := file(File:replace(r"\\ ", " "))
	end
	return Files
end

var SourceTypes := {
	"c" is [c_includes, c_compile]
}

c_program := fun(Executable, Objects, Libraries) do
	var Sources := []
	for Object in Objects or [] do
		for Extension, Functions in SourceTypes do
			var Source := Object % Extension
			if Source:exists then
				Sources:put(Source)
				var Scan := Source:scan("INCLUDES")[PREBUILDS] => Functions[1]
				Object[Source, Scan] => (Functions[2] !! [Source])
				exit
			end
		end
	end
	Executable[Objects, Libraries] => fun(Executable) do
		execute('cc', '-o', Executable, Objects, Libraries, LDFLAGS)
	end
	DEFAULT[Executable]
	ret Executable
end

c_library := fun(Library, Objects, Libraries) do
	var Sources := []
	for Object in Objects or [] do
		for Extension, Functions in SourceTypes do
			var Source := Object % Extension
			if Source:exists then
				Sources:put(Source)
				var Scan := Source:scan("INCLUDES")[PREBUILDS] => Functions[1]
				Object[Source, Scan] => (Functions[2] !! [Source])
				exit
			end
		end
	end
	Library[Objects, Libraries] => fun(Executable) do
		execute('ar', 'rcs', Library, Objects, Libraries)
	end
	DEFAULT[Library]
	ret Library
end

PREFIX := old or file('{getenv("DESTDIR") or ""}{defined("PREFIX") or "/usr/local"}')
INSTALL := meta("install")

install := fun(Source, Target, Mode) do
	Target[Source] => fun(Target) do
		print(Source, " -> ", Target, "\n")
		Target:dir:mkdir
		Source:copy(Target)
		Mode and execute("chmod", Mode, Target)
	end
	INSTALL[Target]
	ret Target
end

TEST := meta("test")

